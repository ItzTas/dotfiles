#!/usr/bin/env zsh
# vim: filetype=zsh

export PATH=$HOME/.local/bin:/usr/local/bin:/usr/bin:$PATH

# a" and i" vim operators
__vi-select-quoted() {
    setopt localoptions noksharrays

    local matching=${${1:-$KEYS}[2]}
    local -i start=CURSOR+2 end=CURSOR+2 found=0 alt=0 count=0

    if ((REGION_ACTIVE )); then
        if (( MARK < CURSOR )); then
            start=MARK+2
        else
        end=MARK+2
        fi
    fi

    [[ $BUFFER[CURSOR+1] = $matching && $BUFFER[CURSOR] != \\ ]] && count=1
    while (( (count || ! alt) && --start )) && [[ $BUFFER[start] != $'\n' ]]; do
        if [[ $BUFFER[start] = "$matching" ]]; then
            if [[ $BUFFER[start-1] = \\ ]]; then
                (( start-- ))
            elif (( ! found )); then
                found=start
            else
                (( ! alt )) && alt=start
                (( count && ++count ))
            fi
        fi
    done

    for (( start=CURSOR+2; ! found && start+1 < $#BUFFER; start++ )); do
        case $BUFFER[start] in
            $'\n') return 1 ;;
            \\) (( start++ )) ;;
            "$matching")
                (( end=start+1, found=start ))
                ;;
        esac
    done

    [[ $BUFFER[end-1] = \\ ]] && (( end++ ))
    until [[ $BUFFER[end] == "$matching" ]]; do
        [[ $BUFFER[end] = \\ ]] && (( end++ ))
        if [[ $BUFFER[end] = $'\n' ]] || (( ++end > $#BUFFER )); then
        end=0
        break
        fi
    done

    if (( alt && (!end || count == 2) )); then
    end=found
    found=alt
    fi
    (( end )) || return 1

    [[ ${${1:-$KEYS}[1]} = a ]] && (( found-- )) || (( end-- ))
    (( REGION_ACTIVE = !!REGION_ACTIVE ))
    [[ $KEYMAP = vicmd ]] && (( REGION_ACTIVE && end-- ))
    MARK=found
    CURSOR=end
}

# git
if (( ${+commands[git]} )); then
	fe() {
		if ! git rev-parse --is-inside-work-tree &>/dev/null; then
			echo "Error: not in a git directory"
			return 1
		fi

		local b
		b="$(git branch -a | grep -v '\->' | sed 's|remotes/origin/||' | sed 's|^\* ||' | sed 's/^ *//;s/ *$//' | sort -u)"

		echo "$b" | fzf --bind 'ctrl-y:execute(git switch {})+abort'
	}

    # overwrite git binary for my own modifications
    git() {
        local args=()
        local has_diff=false
        local no_pager=false

        for arg in "$@"; do
            if [[ "$arg" == "--no-pager" ]]; then
                no_pager=true
            elif [[ "$arg" == "diff" ]]; then
                has_diff=true
            else
                args+=("$arg")
            fi
        done
        if $has_diff && $no_pager && (( ${+commands[diff-so-fancy]} )); then
            command git diff "${args[@]}" | diff-so-fancy
        else
            command git "$@"
        fi
    }
fi

# tmux
if (( ${+commands[tmux]} )); then
    tmux_manager() {
      local session_count
      session_count=$(tmux list-sessions 2>/dev/null | wc -l | tr -d ' ')

      if (( session_count > 0 )); then
        tmux attach
      else
        tmux new-session -s default
      fi
    }
fi

# sesh
if (( ${+commands[sesh]} )); then
	sesh_connect() {
		local selection
		selection=$(
			sesh list --icons | fzf-tmux -p 90%,70% \
				--no-sort --ansi --border-label ' sesh ' --prompt 'âš¡  ' \
				--header '  ^a all ^t tmux ^g configs ^x zoxide ^d tmux kill ^f find' \
				--bind 'tab:down,btab:up' \
				--bind 'ctrl-a:change-prompt(âš¡  )+reload(sesh list --icons)' \
				--bind 'ctrl-t:change-prompt(ðŸªŸ  )+reload(sesh list -t --icons)' \
				--bind 'ctrl-g:change-prompt(âš™ï¸  )+reload(sesh list -c --icons)' \
				--bind 'ctrl-x:change-prompt(ðŸ“  )+reload(sesh list -z --icons)' \
				--bind 'ctrl-f:change-prompt(ðŸ”Ž  )+reload(fd -H -d 2 -t d -E .Trash . ~)' \
				--bind 'ctrl-d:execute(chmod +x ~/.config/sesh/scripts/kill_safe.sh && ~/.config/sesh/scripts/kill_safe.sh {2..})+change-prompt(âš¡  )+reload(sesh list --icons)' \
				--bind 'ctrl-i:preview-up,ctrl-u:preview-down' \
				--bind 'ctrl-y:accept' \
				--preview-window 'right:50%' \
				--preview 'sesh preview {}'
		)
		[[ -n "$selection" ]] && sesh connect "$selection"
	}

	sc() {
		sesh clone "$@"
		local session_name="Projects ðŸ“‚"
		if tmux has-session -t "$session_name"; then
			tmux kill-session -t "$session_name"
		fi
	}
fi

# docker
if (( ${+commands[docker]} )); then
	fdex() {
		local lines container

		lines="$(docker ps --format "id: {{.ID}}\t name: {{.Names}}\t img: {{.Image}}")"
		container="$(echo "$lines" | fzf --header "exec -it")"

		if [ "$container" != "" ]; then
			local container_name
			container_name=$(echo "$container" | awk '{print $4}')

			docker exec -it "$container_name" bash
		fi
	}
fi

# system update
update_zsh_plugins() {
    local plugins_dir="$HOME/.config/zsh/plugins/repos"
    local plugin

    for plugin in "$plugins_dir"/*; do
        if [[ ! -d "$plugin/.git" ]]; then
            continue
        fi
        local plugin_name
        plugin_name=$(basename "$plugin")
        echo "Updating plugin: $plugin_name..."
        git -C "$plugin" pull >/dev/null 2>&1 &
    done

    wait
    echo "All plugins updated."
}

upgrady() {
	(
		# set -e
		trap 'exit 0' SIGINT
		{
			if command -v arch-update; then
				nohup arch-update --check >/dev/null 2>&1 &
				disown
			fi
		} >/dev/null 2>&1

		if command -v paru >/dev/null; then
			paru -Syu --devel
			paru -Fy
		fi
		pacclean

		if command -v flatpak >/dev/null; then
			flatpak update
			flatpak uninstall --unused
		fi

		if command -v freshclam >/dev/null; then
			sudo freshclam
		fi

        if command -v hyprpm >/dev/null; then
		    hyprpm update
        fi

        if command -v omz >/dev/null; then
            omz update
        fi
        update_zsh_plugins
		yadm_update

		{
			if command -v arch-update; then
				nohup arch-update --check >/dev/null 2>&1 &
				disown
			fi
		} >/dev/null 2>&1
	)
}

# pacman & utils
pacclean() {
	local orphans
	orphans=$(pacman -Qdtq)

	if [[ -z $orphans ]]; then
		echo "No orphan packages found."
		return
	fi

	local valid_pkgs=()
	for pkg in "${orphans[@]}"; do
		if pacman -Q "$pkg" &>/dev/null; then
			valid_pkgs+=("$pkg")
		fi
	done

	if [[ ${#valid_pkgs[@]} -eq 0 ]]; then
		echo "No valid orphan packages found."
	else
		sudo pacman -Rns "${valid_pkgs[@]}"
	fi
}

# gtk
setgtktheme() {
	gsettings set org.gnome.desktop.interface gtk-theme "$1"
}

setgtkicons() {
	gsettings set org.gnome.desktop.interface icon-theme "$1"
}

# proto & langs
if (( ${+commands[proto]} )); then
	pgo() {
		proto run go -- "$@"
	}

	pnode() {
		proto run node -- "$@"
	}

	pbun() {
		proto run bun -- "$@"
	}

	pdeno() {
		proto run deno -- "$@"
	}

	pgleam() {
		proto run gleam -- "$@"
	}

	ppython() {
		proto run python -- "$@"
	}
fi

# miru
if (( ${+commands[miru]} )); then
	mggo() {
		miru go "https://github.com/$1"
	}
fi

# file extraction
ex() {
	if [ -f "$1" ]; then
		case "$1" in
		*.tar.bz2) tar xjf "$1" ;;
		*.tar.gz) tar xzf "$1" ;;
		*.bz2) bunzip2 "$1" ;;
		*.rar)
			if (( ${+commands[rar]} )); then
				rar x "$1"
			elif (( ${+commands[unrar]} )); then
				unrar x "$1"
			else
				echo "Please install 'rar' or 'unrar' to extract RAR files."
			fi
			;;
		*.gz) gunzip "$1" ;;
		*.tar) tar xf "$1" ;;
		*.tbz2) tar xjf "$1" ;;
		*.tgz) tar xzf "$1" ;;
		*.zip) unzip "$1" ;;
		*.Z) uncompress "$1" ;;
		*.7z) 7za e "$1" ;;
		*.deb) ar x "$1" ;;
		*.tar.xz) tar xf "$1" ;;
		*.tar.zst) unzstd "$1" ;;
		*) echo "Cannot extract '$1' with ${FUNCNAME[0]}()" ;;
		esac
	else
		echo "'$1' is not a valid file."
	fi
}

lse() {
	if [ -f "$1" ]; then
		case "$1" in
		*.tar.bz2 | *.tbz2) tar tjf "$1" ;;
		*.tar.gz | *.tgz) tar tzf "$1" ;;
		*.tar.xz) tar tJf "$1" ;;
		*.tar.zst) unzstd -c "$1" | tar tf - ;;
		*.tar) tar tf "$1" ;;
		*.bz2) bunzip2 -l "$1" ;;
		*.gz) gzip -l "$1" ;;
		*.zip) unzip -l "$1" ;;
		*.rar)
			if (( ${+commands[rar]} )); then
				rar l "$1"
			elif (( ${+commands[unrar]} )); then
				unrar l "$1"
			else
				echo "Install rar or unrar to list .rar files"
			fi
			;;
		*.7z) 7z l "$1" ;;
		*.Z) uncompress -c "$1" | wc -c ;;
		*.deb) ar t "$1" ;;
		*) echo "'$1' cannot be listed by ${FUNCNAME[0]}()" ;;
		esac
	else
		echo "'$1' is not a valid file"
	fi
}

# memory testing
test_memory() {
	local memory="$1"
	if [[ $memory == "" ]]; then
		echo "usage test_memory <num>G"
		return
	fi
	</dev/zero head -c "$memory" | tail
}

# python
if (( ${+commands[python]} )); then
lsvenv() {
    ls -1 $VENV_HOME
}

venv() {
    if [ $# -eq 0 ]
    then
        echo "Please provide venv name"
    else
        source "$VENV_HOME/$1/bin/activate"
    fi
}

mkvenv() {
    if [ $# -eq 0 ]
    then
        echo "Please provide venv name"
    else
        python3 -m venv $VENV_HOME/$1
    fi
}

rmvenv() {
    if [ $# -eq 0 ]
    then
        echo "Please provide venv name"
    else
        rm -r $VENV_HOME/$1
    fi
}
fi

# others
copybuffer () {
  if builtin which wl-copy &>/dev/null; then
    printf "%s" "$BUFFER" | wl-copy
  else
    zle -M "wl-copy not found. Please make sure you have it installed"
  fi
}

